# Проверка орфографии средствами Yandex Speller

**Проверка орфографии средствами Yandex Speller с опциями и коррекцией ошибок. Возможность вызова как с сервера, так и с клиента (с последующим исправлением).**

# Проверка орфографии средствами Yandex Speller

## Описание

![img](https://infostart.ru/upload/iblock/d07/d07a04048ee8676b17cee4780dc6a06d.png)Обработка производит проверку передаваемого текста средствами API Yandex Speller, довольно проста в использовании.

Может принимать различные параметры и опции для проверки (см. скриншоты). Например может игнорировать короткие слова, исключать переданые слова из строки с разделителями или заданого справочника.

Позволяет на основе распознаных ошибок создавать справочник-словарь исключений.

Строка может содержать как обычный текст, так и HTML разметку.

Опции:

1. Пропускать верхний регистр
2. Пропускать слова с цифрами
3. Пропускать пути и ссылки
4. Выявлять повторы слов
5. Пропускать латиницу
6. Не предлагать замену (только подсвечивать ошибки)
7. Отмечать латиницу
8. Распознавать текст как набор слов
9. Пропускать рЕгИСтР
10. Пропускать римские цифры

Есть возможность при отсутствии ошибок в переданном тексте автоматически закрывать форму.

Проверяет русский, английский и украинский языки.

Ошибки подсвечиваются оранжевым цветом, а при наведении пишется в чем заключается ошибка, при щелчке на нее открывается выпадающий список, позволяющий выбрать из доступных вариантов.

Обращение происходит через HTTP GET (Я использовал бы Web сервисы, но платформа накладывает определенные ограничения, тем не менее в модуле есть закоменченный кусочек для соединения через WS).

Производиться десять попыток достучаться до сервиса, прежде чем будет выведено сообщение о невозможности использования (сервис лег, ну или что вероятнее - ваш интернет).

Если проверка инициирована с клиента (через форму), то в случае нахождения ошибок будет возможность исправить их из предложенных вариантов в выпадающем меню.

Если проверка инициирована с сервера, то результат вернет довольно обширный набор данных, включая исправленный текст, информацию об ошибках и предложенные варианты исправления.

Обработка использует только средства платформы, тестировалась на "голой" конфигурации 8.3.10.2561 УФ (Тонкий клиент), Win Server 2012R2.

На вскидку должна запуститься на чем угодно начиная с *8.3.6.1977* с отключенным режимом совместимости *(Очень уж люблю СтрШаблон() ).*

*Красивости расчитаны на наличие IE-11 в системе, если у Вас все плохо, то поправьте следующую строку в модуле объекта: <meta http-equiv='X-UA-Compatible' content='IE=11'>*

Спроектирована без  использования модальных окон и вызовов.

Обработка довольно подробно документирована, при вызове функции модуля объекта - все параметры подписаны.

Код открыт, можете править в свое удовольствие, только не продавайте отдельно саму обработку (только если в составе продукта).

 

## Передаваемые параметры и полученный результат

Обязательным является только параметр "Текст" при инициализации обработки на сервере, так что не пугайтесь обилию опций, все они могут быть опущены и будут выставлены по умолчанию.

Просто посмотрите, что возвращает закрытая форма или функция, там довольно интуитивно понятно) 

```
 В форму могут быть переданы следующие параметры (структурой):
    Текст (строка)                    -    Текст на проверку (обязателен, иначе не имеет смысла)
    Язык (строка)                    -    Языки проверки (перечисляются через запятую).
                                        Возможные значения:
                                            ru – русский;
                                            uk – украинский;
                                            en – английский.
                                        По умолчанию: "ru,en"
    Формат (строка)                    -    Формат проверяемого текста.
                                        Возможные значения:
                                            plain - текст без разметки (значение по умолчанию);
                                            html - HTML-текст.
    МинДлинаСлова (число)            -    Минимальная длина слова подлежащего проверке.
                                        По умолчанию 0 (проверять все слова).
    Словарь (строка)                -    Если заданы метаданные словаря (вида: "Справочник.Словарь"), 
                                        то будут исключаться из ошибочных слова содержащиеся в наименованиях справочника "Словарь".
                                        Если передана строка с разделителями ";", то вместо справочника словаря будет использоваться она.
                                        По умолчанию пустая строка.
    Опции (число)                    -    Опции Яндекс.Спеллера.
                                        Значением параметра является сумма значений требуемых опций,
                                        см. https://tech.yandex.ru/speller/doc/dg/reference/speller-options-docpage/
                                        Например, Опции = 5 - это сумма опций IGNORE_UPPERCASE и IGNORE_URLS.
                                        По умолчанию Опции = 31 (IGNORE_UPPERCASE, IGNORE_DIGITS, IGNORE_URLS, FIND_REPEAT_WORDS, IGNORE_LATIN).
    ЗакрытьЕслиБезОшибок (булево)    -    Если при проверке не будет обнаружено ошибок - форма будет автоматически закрыта. 
 Форма возвращает либо Неопределено (отказ от изменений), либо Структуру состоящую из:
    Текст (строка)                -    Подкоректированый текст
    БезОшибок (булево)            -    Истина - не осталось ошибок, Ложь - некоторые ошибки были проигнорированы
    КоличествоОшибок (число)    -    Количество проигнорированых ошибок
```

  

```
 ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~
                        Yandex speller 
 ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~
 Проверка орфографии средствами Yandex speller через HTTP GET
 https://tech.yandex.ru/speller/doc/dg/concepts/speller-overview-docpage/
 Функция возвращает структуру со следующими парами Ключ-Значение:
    БезОшибок, КоличествоОшибок, ИсходныйТекст, ИсправленныйТекст, ШаблонТекста, Ошибки
    БезОшибок (булево)            - Текст не содержит/содержит ошибки.
    КоличествоОшибок (число)    - Количество ошибок в тексте
    ИсходныйТекст (строка)        - Текст переданый в функцию
    ИсправленныйТекст (строка)    - Текст с исправленными ошибками (ошибки заменены на первую подсказку)
    ШаблонТекста                - Текст в котором места ошибок обрамлены в "ИндексОшибки" 
    Ошибки (массив структур)    - Состоит из КодОшибки (число), 
                                    ОписаниеОшибки (строка), ПозицияСлова (число), НомерСтроки (число), 
                                    НомерСтолбца (число), ДлинаСлова (число), Подсказки (массив строк) 
 В случае неудачи функция возвращает "Неопределено" и выводит сообщение об ошибке.
 Параметры:
    Текст                        - [обязательный](строка)
                                параметром передается текст на проверку
    Язык                        - [необязательный](строка)
                                Языки проверки (перечисляются через запятую).
                                Возможные значения:
                                    ru – русский;
                                    uk – украинский;
                                    en – английский.
                                По умолчанию: "ru,en".
    Опции                        - [необязательный](число)
                                Опции Яндекс.Спеллера.
                                Значением параметра является сумма значений требуемых опций,
                                см. https://tech.yandex.ru/speller/doc/dg/reference/speller-options-docpage/
                                Например, Опции = 5 - это сумма опций IGNORE_UPPERCASE и IGNORE_URLS.
                                По умолчанию Опции = 31 (IGNORE_UPPERCASE, IGNORE_DIGITS, IGNORE_URLS, FIND_REPEAT_WORDS, IGNORE_LATIN).                                
    Формат                        - [необязательный](строка)
                                Формат проверяемого текста.
                                Возможные значения:
                                    plain - текст без разметки (значение по умолчанию)
                                    html - HTML-текст.
    ИсправлятьИсходныйТекст        - [необязательный](булево) 
                                если Истина, то исходный текст исправляется
                                может быть полезно, если использовать как процедуру и возвращать первый параметр "Текст".
                                По умолчанию Ложь.
    СообщатьОбОшибках            - [необязательный](булево) 
                                если Истина (значение по умолчанию), то будут выводиться сообщения об ошибках
    Словарь                        - [необязательный](строка) 
                                если заданы метаданные словаря (вида: "Справочник.Словарь"), 
                                то будут исключаться из ошибочных слова содержащиеся в наименованиях справочника "Словарь".
                                Если передана строка с разделителями ";", 
                                то вместо справочника словаря будет использоваться она.
                                По умолчанию пустая строка.
    МинДлинаСлова                - [необязательный](число) 
                                минимальная длина слова подлежащего проверке
                                По умолчанию 0 (проверять все слова).
```



##  

## Примеры использования

### **Вариант "на клиенте":**

Предположим вы хотите перед записью документа проверить правописание определенного реквизита, тогда Вы:

1) В модуле формы создаете процедуру "ПередЗаписью()"

2) В ней вызываете форму обработки, передав в нее структуру параметров (обязательно **передайте значение ключа "Текст"**, иначе не имеет смысла, остальное заполниться по умолчанию). Также передайте **владельца формы** - ЭтаФорма.

**!!! ЕСЛИ ВЫ НЕ ПЕРЕДАДИТЕ ВЛАДЕЛЬЦА ФОРМЫ, ТО ОБРАБОТКА ПОСЧИТАЕТ, ЧТО ВЫ ПРОСТО ЗАХОТЕЛИ ПОТЕСТИРОВАТЬ ЕЁ И СЕБЯ И СПРЯЧЕТ КНОПКИ "ОТМЕНИТЬ" и "ПРИНЯТЬ" !!!**

Это сделано специально, но если Вы по какой то причине не хотите передавать владельца, Вы вполне можете закоментить процедуру "ПриОткрытии()" в обработке, это не повлияет на результат, а кнопки будут отображаться всегда.

3) Откройте форму, автоматически пройдет проверка. Если вы передали параметр "ЗакрытьЕслиБезОшибок" - Истина и не обнаружено ошибок, то форма будет сразу закрыта и вернет структуру с результатом проверки.

4) Если были обнаружены ошибки - пользователю будет предложено выбрать верные варианты и согласиться/не согласиться с правками.

5) Нажав кнопку Принять или Отмена (крестик) - форма закроется и вернет владельцу результат проверки или неопределено, соответсвенно.

 

 

```
&НаКлиенте
Процедура ПроверитьОрфографию(Команда)
	СтрокаНаПроверку	= "Мамо мыла раму.";
	Результат			= ОткрытьФормуМодально("Обработка.ПроверкаОрфографии.Форма", Новый Структура("Текст", СтрокаНаПроверку), ЭтаФорма); 	
КонецПроцедуры // ПроверитьОрфографию()
```

 

 

```
&НаКлиенте
Процедура ПроверитьОрфографию(Команда)
	СтрокаНаПроверку	= "Мамо мыла раму.";
	Оповещение			= Новый ОписаниеОповещения("ПослеПроверкиОрфографии", ЭтаФорма, СтрокаНаПроверку); 
	П					= Новый Структура("Текст,Язык,Формат,МинДлинаСлова,Словарь,Опции,ЗакрытьЕслиБезОшибок",
											СтрокаНаПроверку, "ru,en", "plain", 0, "", 31, Истина);
	Форма = ОткрытьФорму("Обработка.ПроверкаОрфографии.Форма", П, ЭтаФорма, Истина, , , Оповещение);

	// Если вдруг надо сказать пользователю, что он все правильно сделал и в его писульке нет ошибок
	Если Форма = Неопределено Или Форма.БезОшибок Тогда
		Сообщение    = Новый СообщениеПользователю;
		Сообщение.Текст    = "Вероятно ошибок нету.";
		Сообщение.Сообщить();
	КонецЕсли; // Если Форма = Неопределено Или Форма.БезОшибок 	
КонецПроцедуры // ПроверитьОрфографию()

// ОБРАБОТКА РЕЗУЛЬТАТА
&НаКлиенте
Процедура ПослеПроверкиОрфографии(РезультатЗакрытия, СтрокаНаПроверку) Экспорт
	Если РезультатЗакрытия <> Неопределено Тогда
		ПоказатьПредупреждение(,СтрШаблон("Проверили орфографию и исправили
								|""%1""
								|
								|на
								|
								|""%2"".%3", 
								СтрокаНаПроверку, 
								РезультатЗакрытия.Текст, 
								?(РезультатЗакрытия.БезОшибок, 
									"",
									Символы.ПС + Символы.ПС + ЧислоПрописью(РезультатЗакрытия.КоличествоОшибок, "Л = ru_RU", "ошибка, ошибки, ошибок, ж,,,,,0"))), 
								60);
	КонецЕсли; // Если РезультатЗакрытия <> Неопределено
КонецПроцедуры // ПослеПроверкиОрфографии() Экспорт
```

 

### **Вариант "на сервере":**

 

Предположим вы хотите лишить пользователя свободы воли выбора и автоматически исправлять ошибки, скажем перед записью документа:

1) В модуле объекта документа создаете процедуру "ПередЗаписью()"

2) Там создаете объект обработки.

3) У этого объекта вызываете единственную функцию YandexSpeller(), в которую можете передать кучу параметров (ну или только текст на проверку первым параметром, только он обязательный), конфигуратор подскажет параметры.

4) Функция вернет структуру с ошибками и всевозможными данными, естественно включая исправленный текст.

 

 

```
&НаСервереБезКонтекста
Процедура ПроверитьОрфографиюНаСервере()
	СтрокаНаПроверку	= "Мамо мыло сраму.";
	Результат			= Обработки.ПроверкаОрфографии.Создать().YandexSpeller(СтрокаНаПроверку);
КонецПроцедуры // ПроверитьОрфографиюНаСервере()
```

 

 

```
&НаСервереБезКонтекста
Процедура ПроверитьОрфографиюНаСервере()
	СтрокаНаПроверку	= "Мамо мыло сраму.";
	Обр					= Обработки.ПроверкаОрфографии.Создать();
	Результат			= Обр.YandexSpeller(СтрокаНаПроверку, "ru,en", 31, "plain", Ложь, Истина, "мыло;сраму", 0);
	
	// ОБРАБОТКА РЕЗУЛЬТАТА
	БезОшибок			= Результат.БезОшибок;
	КоличествоОшибок	= Результат.КоличествоОшибок;
	ИсходныйТекст		= Результат.ИсходныйТекст; 
	ИсправленныйТекст	= Результат.ИсправленныйТекст;
	ШаблонТекста		= Результат.ШаблонТекста;
	мОшибок				= Результат.Ошибки;
КонецПроцедуры // ПроверитьОрфографиюНаСервере()
```

##  

## Листинг модуля объекта

Вы можете скопировать данную функцию в общий модуль или куданибудь еще, она вполне самостоятельна и может работать в отрыве от обработки.

 

 

```
#Область Yandex_speller 
// ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~
//						Yandex speller 
// ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~
// Проверка орфографии средствами Yandex speller через HTTP GET
// https://tech.yandex.ru/speller/doc/dg/concepts/speller-overview-docpage/
//
// Функция возвращает структуру со следующими парами Ключ-Значение:
//	БезОшибок, КоличествоОшибок, ИсходныйТекст, ИсправленныйТекст, ШаблонТекста, Ошибки
//	БезОшибок (булево)			- Текст не содержит/содержит ошибки.
//	КоличествоОшибок (число)	- Количество ошибок в тексте
//	ИсходныйТекст (строка)		- Текст переданый в функцию
//	ИсправленныйТекст (строка)	- Текст с исправленными ошибками (ошибки заменены на первую подсказку)
//	ШаблонТекста				- Текст в котором места ошибок обрамлены в "ИндексОшибки" 
//	Ошибки (массив структур)	- Состоит из КодОшибки (число), 
//									ОписаниеОшибки (строка), ПозицияСлова (число), НомерСтроки (число), 
//									НомерСтолбца (число), ДлинаСлова (число), Подсказки (массив строк) 
// В случае неудачи функция возвращает "Неопределено" и выводит сообщение об ошибке.
//
// Параметры:
//	Текст						- [обязательный](строка)
//								параметром передается текст на проверку
//	Язык						- [необязательный](строка)
//								Языки проверки (перечисляются через запятую).
//								Возможные значения:
//									ru – русский;
//									uk – украинский;
//									en – английский.
//								По умолчанию: "ru,en".
//	Опции						- [необязательный](число)
//								Опции Яндекс.Спеллера.
//								Значением параметра является сумма значений требуемых опций,
//								см. https://tech.yandex.ru/speller/doc/dg/reference/speller-options-docpage/
//								Например, Опции = 5 - это сумма опций IGNORE_UPPERCASE и IGNORE_URLS.
//								По умолчанию Опции = 31 (IGNORE_UPPERCASE, IGNORE_DIGITS, IGNORE_URLS, FIND_REPEAT_WORDS, IGNORE_LATIN).								
//	Формат						- [необязательный](строка)
//								Формат проверяемого текста.
//								Возможные значения:
//									plain - текст без разметки (значение по умолчанию)
//								    html - HTML-текст.
//	ИсправлятьИсходныйТекст		- [необязательный](булево) 
//								если Истина, то исходный текст исправляется
//								может быть полезно, если использовать как процедуру и возвращать первый параметр "Текст".
//								По умолчанию Ложь.
//	СообщатьОбОшибках			- [необязательный](булево) 
//								если Истина (значение по умолчанию), то будут выводиться сообщения об ошибках
//	Словарь						- [необязательный](строка) 
//								если заданы метаданные словаря (вида: "Справочник.Словарь"), 
//								то будут исключаться из ошибочных слова содержащиеся в наименованиях справочника "Словарь".
//								Если передана строка с разделителями ";", 
//								то вместо справочника словаря будет использоваться она.
//								По умолчанию пустая строка.
//	МинДлинаСлова				- [необязательный](число) 
//								минимальная длина слова подлежащего проверке
//								По умолчанию 0 (проверять все слова).
Функция YandexSpeller(	Текст, 
						Знач Язык						= "ru,en", 
						Знач Опции						= 31, 
						Знач Формат						= "plain", 
						Знач ИсправлятьИсходныйТекст	= Ложь, 
						Знач СообщатьОбОшибках			= Истина,
						Знач Словарь					= "",
						Знач МинДлинаСлова				= 0) Экспорт
	// Откидываю все лишнее
	// и не мучаю веб сервис по напрасну.
	Если ТипЗнч(Текст) <> Тип("Строка") Или СтрДлина(Текст)>10000 Тогда
		Сообщение		= Новый СообщениеПользователю;
		Сообщение.Текст	= "Проверьте передаваемый на проверку текст. Он "
		+ ?(ТипЗнч(Текст)<>Тип("Строка"), "не является строкой.", "слишком длинный.");
		Сообщение.Сообщить();
		Возврат Неопределено;
	ИначеЕсли ТипЗнч(Текст) = Тип("Строка") И ПустаяСтрока(Текст) Тогда
		Возврат Новый Структура("БезОшибок,КоличествоОшибок,Ошибки,ИсходныйТекст,ИсправленныйТекст,ШаблонТекста",
		Истина, 0, Новый Массив, Текст, Текст, Текст);
	КонецЕсли;
	
	Попытка
		// Задаю соответсвие для расшифровки кодов ошибок
		КодыОшибок	= Новый Соответствие;
		КодыОшибок.Вставить(1, "Слова нет в словаре");
		КодыОшибок.Вставить(2, "Повтор слова");
		КодыОшибок.Вставить(3, "Неверное употребление прописных и строчных букв");
		КодыОшибок.Вставить(4, "Текст содержит слишком много ошибок");

		// Подключение к Web сервису через SOAP-интерфейс 
		// !!! НЕ ИСПОЛЬЗУЮ, ТК НЕЛЬЗЯ ИСПОЛЬЗОВАТЬ ВСЕ ПАРАМЕТРЫ ЗАПРОСА ИЗ ЗА ОГРАНИЧЕНИЯ ПЛАТФОРМЫ!!!
		//Определения	= Новый WSОпределения("http://speller.yandex.net/services/spellservice?WSDL",,,Неопределено,300);	
		//Прокси		= Новый WSПрокси(Определения, "http://speller.yandex.net/services/spellservice", 
		//									"SpellService", "SpellServiceSoap", Неопределено, 300);
		//Данные		= Прокси.checkText(Текст);
		
		// Долблю 10 раз HTTP Get запрос в надежде получить ответ
		Для Счетчик = 1 По 10 Цикл
			ТекстНаПроверку	= СтрЗаменить(Текст, Символы.Таб,	"%20");
			ТекстНаПроверку	= СтрЗаменить(ТекстНаПроверку, " ", "%20");
			
			URL			= СтрШаблон("services/spellservice/checkText?text=%1&lang=%2&options=%3&format=%4", ТекстНаПроверку, СокрЛП(Язык), СокрЛП(Опции), СокрЛП(Формат));
			//Заголовки	= Новый Соответствие();
			//Заголовки.Вставить("Accept", "text/html,application/xhtml+xml,application/xml;q=0.9,image/webp,image/apng,*/*;q=0.8");
			//Заголовки.Вставить("Accept-Encoding", "gzip, deflate");
			//Заголовки.Вставить("Accept-Language", "en-US,en;q=0.8,ru;q=0.6");
			//Заголовки.Вставить("Cache-Control", "max-age=0");
			//Заголовки.Вставить("Connection", "keep-alive");
			//Заголовки.Вставить("Host", "speller.yandex.net");
			//Заголовки.Вставить("Upgrade-Insecure-Requests", "1");			
			//Заголовки.Вставить("User-Agent", "Mozilla/5.0 (Windows NT 6.3; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/61.0.3163.100 Safari/537.36");
			//Заголовки.Вставить("X-Compress", "null");			
			
			HTTPСоединение	= Новый HTTPСоединение("speller.yandex.net", 80, , , , Счетчик*10);
			//HTTPСоединение= Новый HTTPСоединение("speller.yandex.net", 443,,,, Счетчик*10, Новый ЗащищенноеСоединениеOpenSSL());
			HTTPЗапрос		= Новый HTTPЗапрос(URL); //, Заголовки);		
			HTTPОтвет		= HTTPСоединение.Получить(HTTPЗапрос);
			Если HTTPОтвет.КодСостояния	= 200 Тогда Прервать КонецЕсли;			
		КонецЦикла; // Для Счетчик = 1 По 10 
		Если HTTPОтвет.КодСостояния <> 200 Тогда
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= "Не удалось соединиться с сервисом yandex.speller.
							|Проверьте подключение к интернету.";
			Сообщение.Сообщить();
			Возврат Неопределено;			
		КонецЕсли; // Если HTTPОтвет.КодСостояния <> 200
		
		// Перебираю XML ответ
		ЧтениеXML		= Новый ЧтениеXML();    
		ЧтениеXML.УстановитьСтроку(HttpОтвет.ПолучитьТелоКакСтроку());     
		ПостроительDOM	= Новый ПостроительDOM;
		Попытка 
	    	ДокументDOM		= ПостроительDOM.Прочитать(ЧтениеXML);
		Исключение
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= "Не возможно прочитать ответ сервиса Yandex Speller.
							|Возможно битая кодировка." + Символы.ПС + ОписаниеОшибки();
			Сообщение.Сообщить();
			Возврат Неопределено;						
		КонецПопытки;
		Ошибки			= Новый Массив;
		ОшибкиСтрокой	= "";    
		ЭлементыError	= ДокументDOM.ПолучитьЭлементыПоИмени("Error");
		сАтрибутов		= Новый Соответствие;
		Для Каждого Атрибут Из СтрРазделить("cod:КодОшибки;pos:ПозицияСлова;row:НомерСтроки;col:НомерСтолбца;len:ДлинаСлова", ";", Ложь) Цикл
			сАтрибутов.Вставить(Лев(Атрибут, 3), Сред(Атрибут, 5));	
		КонецЦикла; // Для Каждого Атрибут Из СтрРазделить()
		ДЛЯ КАЖДОГО ЭлементError ИЗ ЭлементыError ЦИКЛ
			Попытка
				Ошибка		= Новый Структура("Подсказки", Новый Массив);
				Для Каждого Атрибут Из ЭлементError.Атрибуты Цикл
				Ошибка.Вставить(сАтрибутов.Получить(Лев(Атрибут.Имя, 3)), Число(Атрибут.Значение));
				КонецЦикла; // Для Каждого Атрибут Из ЭлементError.Атрибуты
				Ошибка.Вставить("ОписаниеОшибки",	КодыОшибок.Получить(Ошибка.КодОшибки)); 
				Слова		= ЭлементError.ПолучитьЭлементыПоИмени("word");
				Если Не Слова.Количество() Тогда Продолжить КонецЕсли;
				Ошибка.Вставить("Слово", Слова[0].ТекстовоеСодержимое);
				ОшибкиСтрокой	= СтрШаблон("%1,""%2""", ОшибкиСтрокой, СтрЗаменить(Ошибка.Слово, """", ""));
				Подсказки	= ЭлементError.ПолучитьЭлементыПоИмени("s");
				Для Каждого Подсказка Из Подсказки Цикл
				Ошибка.Подсказки.Добавить(Подсказка.ТекстовоеСодержимое);
				КонецЦикла; // Для Каждого Подсказка Из Подсказки    
				Ошибки.Добавить(Ошибка);
			Исключение
				Сообщение		= Новый СообщениеПользователю;
				Сообщение.Текст	= "Ошибка при разборе ответа от сервера. Итерация будет пропущена." + Символы.ПС + ОписаниеОшибки();
				Сообщение.Сообщить();
			КонецПопытки;
		КОНЕЦЦИКЛА; // ДЛЯ КАЖДОГО ЭлементError ИЗ ЭлементыError
		ОшибкиСтрокой	= Сред(ОшибкиСтрокой, 2);
		
		// Немного прибираюсь за собой
		Заголовки = Неопределено; HTTPСоединение = Неопределено; 
		HTTPЗапрос = Неопределено; HTTPОтвет = Неопределено;
		ЧтениеXML.Закрыть(); ЧтениеXML = Неопределено; ДокументDOM = Неопределено; 
		ПостроительDOM = Неопределено; сАтрибутов = Неопределено;
				
		// Если надо найти слова-исключения из справочника
		ЕСЛИ ТипЗнч(Словарь) = Тип("Строка") И СтрДлина(ОшибкиСтрокой) И СтрДлина(Словарь) И СтрНачинаетсяС(Словарь, "Справочник.") ТОГДА 		
			#Если НаСервере Тогда				
			Если Метаданные.Справочники.Найти(Сред(Словарь, 12)) <> Неопределено Тогда				
				Запрос	= Новый Запрос("
				|ВЫБРАТЬ Наименование
				|ИЗ "+Словарь+"
				|ГДЕ Наименование В("+ОшибкиСтрокой+")");
				Словарь	= Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Наименование");
			КонецЕсли;
			#КонецЕсли
		ИНАЧЕЕСЛИ ТипЗнч(Словарь) = Тип("Строка") ТОГДА // Если словарь передан строкой 
			Словарь = СтрРазделить(Словарь, ";", Ложь);
		КОНЕЦЕСЛИ; // ЕСЛИ ТипЗнч(Словарь) = Тип("Булево") И Словарь
		
		// Удаляю из ошибок короткие и словарные слова
		КолвоЭлтовКоллекции	= Ошибки.Количество(); 
		ДЛЯ оИндекс = 1 ПО КолвоЭлтовКоллекции ЦИКЛ
			Индекс		= КолвоЭлтовКоллекции - оИндекс;
			Ошибка		= Ошибки[Индекс];
			// Если слово слишком короткое, чтоб быть ошибкой
			Если МинДлинаСлова И Ошибка.ДлинаСлова < МинДлинаСлова Тогда 
				Ошибки.Удалить(Индекс);					
			ИначеЕсли ТипЗнч(Словарь) = Тип("Массив") Тогда
				// Если слово является словарным (исключением)
	
				Для Каждого Слово Из Словарь Цикл
					Если СокрЛП(Нрег(Ошибка.Слово)) = СокрЛП(Нрег(Слово)) Тогда
						Ошибки.Удалить(Индекс); Прервать; 
					КонецЕсли; // Если СокрЛП(Нрег(Ошибка.word)) = СокрЛП(Нрег(Слово))
				КонецЦикла; // Для Каждого Слово Из Словарь 		
			КонецЕсли; // Если Не МинДлинаСлова И Ошибка.len < МинДлинаСлова
		КОНЕЦЦИКЛА; // ДЛЯ оИндекс = 1 ПО КолвоЭлтовКоллекции
		
		// Перебираю все ошибки
		ИсходныйТекст		= Текст;
		ИсправленныйТекст	= Текст;
		ДЛЯ КАЖДОГО Ошибка ИЗ Ошибки ЦИКЛ
			Подсказки	= "";
			// Перебираю все доступные подсказки 
			// для уведомления пользователя о доступных вариантах
			Для Каждого Подсказка Из Ошибка.Подсказки Цикл
				Подсказки	= Подсказки + Подсказка + ", ";
			КонецЦикла;
			Подсказки	= Лев(Подсказки, СтрДлина(Подсказки)-2);
			Если Ошибка.Подсказки.Количество() Тогда
				ИсправленныйТекст	= СтрЗаменить(ИсправленныйТекст, Ошибка.Слово, Ошибка.Подсказки[0]); 	
			КонецЕсли; // Если Ошибка.Подсказки.Количество()			
			Если СообщатьОбОшибках Тогда
				// Непосредственно вывод по ошибке
				Сообщение		= Новый СообщениеПользователю;
				Сообщение.Текст	= СтрШаблон("Ошибка в слове: ""%1"". %2. %3", 
									Ошибка.Слово, 
									Ошибка.ОписаниеОшибки,
									?(Ошибка.Подсказки.Количество(), 
										Символы.ПС + "Возможно Вы хотели написать: " + Подсказки, ""));
				Сообщение.Сообщить();
			КонецЕсли; // Если СообщатьОбОшибках
		КОНЕЦЦИКЛА; // ДЛЯ КАЖДОГО Ошибка ИЗ Данные.error
		
		Если ИсправлятьИсходныйТекст Тогда Текст = ИсправленныйТекст КонецЕсли;
		
		// Создаю шаблон строки с ошибками
		// где индекс массива ошибок соответсвует тегу Индекс
		ШаблонТекста	= ИсходныйТекст;
		КолвоЭлтовКоллекции	= Ошибки.Количество(); 
		Для оИндекс = 1 По КолвоЭлтовКоллекции Цикл
			Индекс			= КолвоЭлтовКоллекции - оИндекс;
			Ошибка			= Ошибки[Индекс];
			ШаблонТекста	= СтрШаблон("%1%2%3",
							Лев(ШаблонТекста, Ошибка.ПозицияСлова), Индекс, Сред(ШаблонТекста, Ошибка.ПозицияСлова+Ошибка.ДлинаСлова+1));			
		КонецЦикла; // Для оИндекс = 1 По КолвоЭлтовКоллекции
		
		Возврат Новый Структура("БезОшибок,КоличествоОшибок,Ошибки,ИсходныйТекст,ИсправленныйТекст,ШаблонТекста",
								Не Булево(Ошибки.Количество()), Ошибки.Количество(), Ошибки, ИсходныйТекст, ИсправленныйТекст, ШаблонТекста);			
	Исключение
		Сообщение		= Новый СообщениеПользователю;
		Сообщение.Текст	= "Не удалось проверить орфографию." + Символы.ПС + ОписаниеОшибки();
		Сообщение.Сообщить();
		Возврат Неопределено;
	КонецПопытки;
КонецФункции // YandexSpeller()
// ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~ ~~~~~
#КонецОбласти
```

## ![img](https://infostart.ru/upload/iblock/176/1767afe5f0ddfbc59729d3b495fd2473.png)

## Ответы на вопросы

- **Q**: Подскажите как сделать полосу прокрутки в поле HTML?
- **A**: В модуле формы, в процедуре "СобратьHTML()" сотрите "overflow: hidden;"

 

- **Q**: Почему при вызове формы пропадают кнопки "Принять" и "Отмена"

- **A**

  : При создании формы обработки ПроверкиОрфографии из другой формы - не забывайте передавать параметром владельца "ЭтаФорма"

   

- **Q**:  У меня очень длинные строчки с текстом, как настроить их авто перенос?

- **A**: В модуле формы, в процедуре "СобратьHTML()" добавьте в блок <style></style> вариации следующего (можете посмотреть в гугле за что отвечает каждая строка).

```
pre {
	white-space: pre-line;
	word-wrap: break-word;
	word-break: break-all;
	width: 100%;
}
```

### TODO:
* При проверке справочника запоминать исправленные слова и проверять из этого соответсвия до проверки яндексом  
* Использовать асинхронную проверку  
* Отображать статус проверки  
* Добавить описание в модуль объекта для типовых конфигураций  
* Возможность проверять не только Наименование справочника, но и любой другой строковый реквизит   